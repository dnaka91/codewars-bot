username = @{ (ASCII_ALPHANUMERIC | PUNCTUATION)+ }
add = { "add" ~ username }
remove = { ("remove" | "rm") ~ username }

date = @{ ASCII_DIGIT{4} ~ "/" ~ ASCII_DIGIT{1,2} ~ "/" ~ ASCII_DIGIT{1,2} }
stats = { "stats" ~ ("since" ~ date)? }
help = { "help" }

weekday = @{ (
	^"mon" ~ ^"day"?
    | ^"tue" ~ ^"sday"?
    | ^"wed" ~ ^"nesday"?
    | ^"thu" ~ ^"rsday"?
    | ^"fri" ~ ^"day"?
    | ^"sat" ~ ^"urday"?
    | ^"sun" ~ ^"day"?
) }
time = @{ ASCII_DIGIT{2} ~ ":" ~ ASCII_DIGIT{2} }

schedule = { "schedule" ~ "on" ~ weekday ~ ("at" ~ time)? }

command = { SOI ~ (add | remove | stats | help | schedule) ~ EOI }

WHITESPACE = _{ " " | "\n" }
